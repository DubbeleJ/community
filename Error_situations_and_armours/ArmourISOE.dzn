/* armouring at the requires side (interface IStrict)
 * create a robust copy of the requires interface IRobust
 * create an armouring component Armour
 * 
 * IllegalSynchronousOutEvent_
 * context is expected to not generate synchronous out-event via requires interface
 * but may still do that
 */
import Logger.dzn;

interface IStrict
{
  in void ia();
  out void oa();

  behaviour
  {
    on ia: {}
    on inevitable: oa;
  }
}

interface IRobust
{
  in void ia();
  out void oa();

  behaviour
  {
    on ia: {}
    on ia: oa;
    on inevitable: oa;
  }
}

component ArmourISOE {
	provides IStrict pStrict;
	requires IRobust rRobust;
	requires injected ILogger iLog;

	behaviour {	
		requires dzn.async p;
				
		bool requested = false;
		on pStrict.ia(): {
			rRobust.ia(); 
			if (!requested) {p.req(); requested = true;}
		}
		on rRobust.oa(): {
			if (!requested) pStrict.oa();
			else {
				requested = false; p.clr();
				iLog.Log($"Native comp does sync out-event oa where async oa was expected"$);
			}
		}
		on p.ack(): requested = false;
    }
}

component Dezyne {
	provides IA pp;
	requires IStrict rStrict;

	behaviour {			
		on pp.ia(): {rStrict.ia();}
		on rStrict.oa(): {pp.oa();}
    }
}

interface IA
{
  in void ia();
  out void oa();

  behaviour
  {
    on ia: {}
    on inevitable: oa;
  }
}

component Native {
	provides IRobust pRobust;
	requires IRobust r;
}

component ArmouredSystemISOE {
	provides IA pp;
	requires IRobust ir;
	
	system {
		Dezyne re;
		ArmourISOE am;
		Native n;
		
		pp <=> re.pp;
		re.rStrict <=> am.pStrict;
		am.rRobust <=> n.pRobust;
		n.r <=> ir;
	}
}

