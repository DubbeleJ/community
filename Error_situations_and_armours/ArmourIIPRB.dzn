/* armouring at the provides side (interface IStrict)
 * create a robust copy of the provides interface IRobust
 * create an armouring component Armour
 * 
 * IllegalInProvidesReturnsBool_
 * a provided interface may have an illegal in-event with a bool return type
 */
import Logger.dzn;

interface IStrict
{
  in bool ia();
  out void oa();

  behaviour
  {
    on ia: illegal;
    on inevitable: oa;
  }
}

interface IRobust
{
  in bool ia();
  out void oa();

  behaviour
  {
    on ia: {reply(false);}
    on inevitable: oa;
  }
}

component ArmourIIPRB // IStrict to IRobust
{
  provides IRobust pRobust;
  requires IStrict rStrict;
  requires injected ILogger iLog;
  
  behaviour {
    on pRobust.ia(): {reply(false); iLog.Log($"In-event ia is illegal in port"$);} // armour illegal ia
    on rStrict.oa(): pRobust.oa();  
  }
}

component Dezyne
{
  provides IStrict pStrict;
  requires IB rp;

  behaviour
  {
    on pStrict.ia(): illegal;
    on rp.oa(): pStrict.oa();  
  }
}

interface IB
{
  in void ia();
  out void oa();

  behaviour
  {
    on ia: illegal;
    on inevitable: oa;
  }
}

component Native {
	provides IRobust p;
	requires IRobust rRobust;
}

component ArmouredSystemIIPRB {
	requires IB pp;
	provides IRobust ir;
	
	system {
		Dezyne ai;
		ArmourIIPRB am;
		Native n;
		
		pp <=> ai.rp;
		am.rStrict <=> ai.pStrict;
		n.rRobust <=> am.pRobust;
		ir <=> n.p;
	}
} 